alarisGP_oldFW: THEORY
 BEGIN

  %-- constants & type definitions --------------------------------------------------------------------------------
  max: nonneg_real = 1200
  min: nonneg_real = 0.01
  alaris_real: TYPE = {x: nonneg_real | x <= max}

  % note: the tick rate accelerates when timer = 0 -- we are not modelling this for now.
  tick: TYPE = nat
  max_timer: tick = 5
  alaris_timer: TYPE = upto(max_timer)
  decrement(t: alaris_timer, n: tick): alaris_timer = IF t - n >= 0 THEN t - n ELSE t ENDIF

  time: TYPE = nonneg_real % milliseconds
  time2tick(t: time): tick

  tiny_step: alaris_real = 0.1
  small_step: alaris_real = 1
  big_step: alaris_real = 10
  alaris_step: TYPE = {x: alaris_real | x = small_step OR x = big_step OR x = tiny_step}

  state: TYPE = [# display: alaris_real, timer: alaris_timer, step: alaris_step #]
  %----------------------------------------------------------------------------------------------------------------


  %-- utility functions -------------------------------------------------------------------------------------------
  trim(x: real): alaris_real = IF x > max THEN max ELSIF x < 0 THEN 0 ELSE x ENDIF
  ceil(x: real): real = ceiling(x)
  %----------------------------------------------------------------------------------------------------------------


  %-- alaris' chevron (UP,up,dn,DN) -------------------------------------------------------------------------------
  alaris_up(delta: alaris_step, val: alaris_real): alaris_real = 
     IF    val < 100                 THEN trim( floor((val*10) + delta) / 10 )
     ELSIF val >= 100 AND val < 1000 THEN trim( floor((val) + delta) )
     ELSE                                 trim( (floor(val/10) + delta) * 10 ) ENDIF

  alaris_UP(delta: alaris_step, val: alaris_real): alaris_real = 
     IF    val < 100                 THEN trim( floor(val) + delta )
     ELSIF val >= 100 AND val < 1000 THEN trim( (floor(val/10) + delta) * 10 )
     ELSE                                 trim( (floor(val/100) + delta) * 100 ) ENDIF

  %-- note: dn(100) = 99 (and not 99.9)
  alaris_dn(delta: alaris_step, val: alaris_real): alaris_real = 
     IF    val < 100                THEN trim( (ceil(val*10) - delta) / 10 )
     ELSIF val >= 100 AND val < 1000  THEN trim( ceil(val - delta) )
     ELSE                                 trim( (ceil(val/10) - delta) * 10 ) ENDIF

  %-- the conditions for DN are apparently slightly different from those of the other buttons...
  alaris_DN(delta: alaris_step, val: alaris_real): alaris_real = 
     IF    val < 100                THEN trim( ceil(val) - delta)
     ELSIF val >= 100 AND val < 1000  THEN trim( (ceil(val/10) - delta) * 10 )
     ELSE                                 trim( (ceil(val/100) - delta) * 100 ) ENDIF
  %----------------------------------------------------------------------------------------------------------------


  %-- debug utils -------------------------------------------------------------------------------------------------
  state2string(st: state): string =
	""
  %    "{ display:" + display(st)  + ", " + " timer:" + timer(st) + ", " + " step:" + step(st) + " }"
  %----------------------------------------------------------------------------------------------------------------


  %-- UP ----------------------------------------------------------------------------------------------------------
  % This is the behaviour of the old firmware. In the new firmware, the current step depends on the previous step.
  press_UP_1(st: state): state =
      LET t: alaris_timer = decrement(timer(st), 1),
          s: alaris_step  =
            IF t = 0 AND step(st) = small_step 
                 AND ((display(st) < 100 AND fractional(display(st) / 10 ) = 0) 
                        OR (display(st) > 100 AND fractional(display(st) / 100 ) = 0))
              THEN big_step
            ELSE step(st) ENDIF,
          d: alaris_real = alaris_UP(s, display(st)),
          dbg = println(state2string(st))
        IN st WITH [display := d, timer := t, step := s]

  press_UP(n: tick, st: state): RECURSIVE state =
      IF n = 0 THEN st WITH [ timer := max_timer ]
      ELSE LET st_prime = press_UP_1(st) IN press_UP( n - 1, st_prime) ENDIF
    MEASURE n

  click_UP(st: state): state = press_UP(1, st)
  %----------------------------------------------------------------------------------------------------------------


  %-- up ----------------------------------------------------------------------------------------------------------
  % This is the behaviour of the old firmware. In the new firmware, the current step depends on the previous step.
  press_up_1(st: state): state =
      LET d: alaris_real = alaris_up(small_step, display(st)),
          dbg = println(state2string(st))
        IN st WITH [ display := d ]

  press_up(n: tick, st: state): RECURSIVE state =
      IF n = 0 THEN st WITH [ timer := max_timer ]
      ELSE LET st_prime = press_up_1(st) IN press_up( n - 1, st_prime) ENDIF
    MEASURE n

  click_up(st: state): state = press_up(1, st)
  %----------------------------------------------------------------------------------------------------------------


  %-- DN ----------------------------------------------------------------------------------------------------------
  % This is the behaviour of the old firmware. In the new firmware, the current step depends on the previous step.
  press_DN_1(st: state): state =
      LET t: alaris_timer = decrement(timer(st), 1),
          s: alaris_step  =
            COND
               t = 0 AND step(st) = small_step 
                 AND ((display(st) > 10 AND display(st) < 100 AND fractional(display(st) / 10 ) = 0) 
                        OR (display(st) > 100 AND fractional(display(st) / 100 ) = 0))
                    -> big_step,

               t /= max_timer AND step(st) = big_step AND (display(st) = 10 OR display(st) = 100)
                    -> small_step,

               ELSE -> step(st) ENDCOND,
          d: alaris_real = alaris_DN(s, display(st)),
          dbg = println(state2string(st))
        IN st WITH [display := d, timer := t, step := s]

  press_DN(n: tick, st: state): RECURSIVE state =
      IF n = 0 THEN st WITH [ timer := max_timer ]
      ELSE LET st_prime = press_DN_1(st) IN press_DN( n - 1, st_prime) ENDIF
    MEASURE n

  click_DN(st: state): state = press_DN(1, st)
  %----------------------------------------------------------------------------------------------------------------


  %-- dn ----------------------------------------------------------------------------------------------------------
  % This is the behaviour of the old firmware. In the new firmware, the current step depends on the previous step.
  press_dn_1(st: state): state =
      LET d: alaris_real = alaris_dn(small_step, display(st)),
          dbg = println(state2string(st))
        IN st WITH [ display := d ]

  press_dn(n: tick, st: state): RECURSIVE state =
      IF n = 0 THEN st WITH [ timer := max_timer ]
      ELSE LET st_prime = press_dn_1(st) IN press_dn( n - 1, st_prime) ENDIF
    MEASURE n

  click_dn(st: state): state = press_dn(1, st)
  %----------------------------------------------------------------------------------------------------------------


  %-- tests
  init(x: alaris_real): state = (# display := x, timer := max_timer, step := small_step #)

  test1: state = LET dbg = println("press UP, test1") IN press_UP(40, init(18))
  test2: state = LET dbg = println("press UP, test2") IN press_UP(40, init(98))
  test3: state = LET dbg = println("press UP, test3") IN press_UP(40, init(228))

  test4: state = LET dbg = println("press up, test4") IN press_up(40, init(18))
  test5: state = LET dbg = println("press up, test5") IN press_up(40, init(98))
  test6: state = LET dbg = println("press up, test6") IN press_up(40, init(228))

  test7: state = LET dbg = println("press DN, test7") IN press_DN(40, init(1110))
  test8: state = LET dbg = println("press DN, test8") IN press_DN(40, init(112))
  test9: state = LET dbg = println("press DN, test9") IN press_DN(40, init(92))
  test10: state = LET dbg = println("press DN, test10") IN press_DN(40, init(12.4))
  test11: state = LET dbg = println("press DN, test11") IN press_DN(40, init(2.4))

  test12: state = LET dbg = println("press dn, test12") IN press_dn(40, init(1110))
  test13: state = LET dbg = println("press dn, test13") IN press_dn(40, init(112))
  test14: state = LET dbg = println("press dn, test14") IN press_dn(40, init(92))

 END alarisGP_oldFW
